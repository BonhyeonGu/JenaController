PREFIX tsc: <http://paper.9bon.org/ontologies/smartcity/0.2#>
PREFIX sta: <http://paper.9bon.org/ontologies/sensorthings/1.1#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?area ?areaName ?avgTraffic ?avgIlluminance (?avgTraffic / ?avgIlluminance AS ?ratio)
WHERE {
    ?area tsc:hasThing ?thing.
    ?thing sta:hasLocation/sta:haslocation/sta:hascoordinates ?coords.
    
    # Extract latitude and longitude
    BIND(xsd:float(STRBEFORE(?coords, " ")) AS ?latitude)
    BIND(xsd:float(STRBEFORE(STRAFTER(?coords, " "), " ")) AS ?longitude)
    
    # Filter by latitude and longitude
    FILTER(?latitude >= 35.10 && ?latitude <= 35.11)
    FILTER(?longitude >= 128.95 && ?longitude <= 128.97)

    # Subquery to calculate avgTraffic for each area
    {
        SELECT ?area (AVG(?people) AS ?avgTraffic)
        WHERE {
            ?area tsc:hasThing ?thing.
            ?thing sta:hasMultiDatastream/sta:hasObservation ?obsTraffic.
            ?obsTraffic sta:hasresultTime ?resultTime;
                        sta:hasresult ?result.
            ?result sta:hasObservedProperty ?obsPropTraffic;
                    sta:hasvalue ?value.
            ?obsPropTraffic sta:hasname "traffic_volume".
            BIND(xsd:decimal(?value) AS ?people)
        }
        GROUP BY ?area
    }

    # Subquery to calculate avgIlluminance for each area
    {
        SELECT ?area (AVG(?illuminanceValue) AS ?avgIlluminance)
        WHERE {
            ?area tsc:hasThing ?thing.
            ?thing sta:hasMultiDatastream/sta:hasObservation ?obsIlluminance.
            ?obsIlluminance sta:hasresultTime ?resultTime;
                            sta:hasresult ?result.
            ?result sta:hasObservedProperty ?obsPropIlluminance;
                    sta:hasvalue ?value.
            ?obsPropIlluminance sta:hasname "iluminance".
            BIND(xsd:decimal(?value) AS ?illuminanceValue)
        }
        GROUP BY ?area
    }

    # Get the name of the area
    ?area tsc:hasName ?areaName.
}
GROUP BY ?area ?areaName ?avgTraffic ?avgIlluminance
ORDER BY DESC(?ratio)
LIMIT 1