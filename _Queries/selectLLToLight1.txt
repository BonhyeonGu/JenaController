PREFIX tsc: <http://paper.9bon.org/ontologies/smartcity/0.2#>
PREFIX sta: <http://paper.9bon.org/ontologies/sensorthings/1.1#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?area ?areaName ?avgTraffic ?avgIlluminance (?avgTraffic / ?avgIlluminance AS ?ratio)
WHERE {
    ?area tsc:hasThing ?thing .
    ?thing sta:hasLocation/sta:haslocation/sta:hascoordinates ?coords .

    # Extract latitude and longitude
    BIND(xsd:float(STRBEFORE(?coords, " ")) AS ?latitude)
    BIND(xsd:float(STRBEFORE(STRAFTER(?coords, " "), " ")) AS ?longitude)

    # Filter by latitude and longitude
    FILTER(?latitude >= 35.10 && ?latitude <= 35.11)
    FILTER(?longitude >= 128.95 && ?longitude <= 128.97)

    # Subquery to calculate avgTraffic for each area
    {
        SELECT ?area (SUM(?people) / COUNT(?people) AS ?avgTraffic)
        WHERE {
            ?area tsc:hasThing ?thing .
            ?thing sta:hasMultiDatastream/sta:hasIndexpoint ?indexpoint .
            ?indexpoint sta:pointToMultiObservedProperty/sta:hasname "traffic_volume"^^xsd:string .
            ?indexpoint sta:pointToresult ?resultResource .
            ?resultResource sta:hasvalue ?peopleRaw .
            BIND(xsd:decimal(?peopleRaw) AS ?people)
        }
        GROUP BY ?area
    }
    
    # Subquery to calculate avgIlluminance for each area
    OPTIONAL {
        SELECT ?area (SUM(?illuminanceValue) / COUNT(?illuminanceValue) AS ?avgIlluminance)
        WHERE {
            ?area tsc:hasThing ?thing .
            ?thing sta:hasMultiDatastream/sta:hasIndexpoint ?indexpoint .
            ?indexpoint sta:pointToMultiObservedProperty/sta:hasname "iluminance"^^xsd:string .
            ?indexpoint sta:pointToresult ?resultResource .
            ?resultResource sta:hasvalue ?illuminanceValueRaw .
            BIND(xsd:decimal(?illuminanceValueRaw) AS ?illuminanceValue)
        }
        GROUP BY ?area
    }

    # Get the name of the area
    ?area tsc:hasName ?areaName.
}
ORDER BY DESC(?ratio)
LIMIT 1