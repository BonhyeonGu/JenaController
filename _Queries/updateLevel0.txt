PREFIX tsc: <http://paper.9bon.org/ontologies/smartcity/0.2#>
PREFIX sta: <http://paper.9bon.org/ontologies/sensorthings/1.1#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

DELETE WHERE {
    ?area tsc:hasLevel ?oldLevel.
};

INSERT {
    ?area tsc:hasLevel ?level.
} WHERE {
    ?city tsc:hasArea ?area.
    ?area tsc:hasSquareMeter ?sqm.
    {
        SELECT ?area (MAX(?resultTime) AS ?latestTime)
        WHERE {
            ?area tsc:hasThing/sta:hasMultiDatastream/sta:hasObservation ?observation.
            ?observation sta:hasresultTime ?resultTime;
                        sta:hasresult [
                            sta:hasObservedProperty ?obsProp;
                            sta:hasvalue ?count
                        ].
            ?obsProp sta:hasname "Visit".
        }
        GROUP BY ?area
    }
    ?area tsc:hasThing/sta:hasMultiDatastream/sta:hasObservation ?latestObservation.
    ?latestObservation sta:hasresultTime ?latestTime;
                    sta:hasresult [
                        sta:hasObservedProperty ?obsProp;
                        sta:hasvalue ?count
                    ].
    ?obsProp sta:hasname "Visit".

    BIND(xsd:decimal(?count) AS ?people)
    BIND(?people / ?sqm AS ?peoplePerSqM)

    OPTIONAL { ?level tsc:hasName "A" . FILTER(?peoplePerSqM <= 0.5) }
    OPTIONAL { ?level tsc:hasName "B" . FILTER(?peoplePerSqM > 0.5 && ?peoplePerSqM <= 0.7) }
    OPTIONAL { ?level tsc:hasName "C" . FILTER(?peoplePerSqM > 0.7 && ?peoplePerSqM <= 1.08) }
    OPTIONAL { ?level tsc:hasName "D" . FILTER(?peoplePerSqM > 1.08 && ?peoplePerSqM <= 1.39) }
    OPTIONAL { ?level tsc:hasName "E" . FILTER(?peoplePerSqM > 1.39 && ?peoplePerSqM <= 2) }
    OPTIONAL { ?level tsc:hasName "F" . FILTER(?peoplePerSqM > 2) }
}