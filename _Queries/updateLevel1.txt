PREFIX tsc: <http://paper.9bon.org/ontologies/smartcity/0.2#>
PREFIX sta: <http://paper.9bon.org/ontologies/sensorthings/1.1#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

DELETE WHERE {
    ?area tsc:hasLevel ?oldLevel.
};

INSERT {
    ?area tsc:hasLevel ?level.
} WHERE {
    ?city tsc:hasArea ?area.
    ?area tsc:hasSquareMeter ?sqm.
    ?area tsc:hasThing/sta:hasMultiDatastream/sta:hasIndexpoint [
        sta:pointToMultiObservedProperty/sta:hasname "traffic_volume";
        sta:pointToresult ?result
    ].
    
    {
        SELECT ?area (MAX(?resultTime) AS ?latestTime)
        WHERE {
            ?area tsc:hasThing/sta:hasMultiDatastream/sta:hasIndexpoint [
                sta:pointToMultiObservedProperty/sta:hasname "traffic_volume";
                sta:pointToresult/sta:isresultByObservation/sta:hasresultTime ?resultTime
            ].
        }
        GROUP BY ?area
    }
    
    ?result sta:isresultByObservation/sta:hasresultTime ?latestTime;
            sta:hasvalue ?count.

    BIND(xsd:decimal(?count) AS ?people)
    BIND(?people / ?sqm AS ?peoplePerSqM)

    OPTIONAL { ?level tsc:hasName "A" . FILTER(?peoplePerSqM <= 0.3077) }
    OPTIONAL { ?level tsc:hasName "B" . FILTER(?peoplePerSqM > 0.3077 && ?peoplePerSqM <= 0.431) }
    OPTIONAL { ?level tsc:hasName "C" . FILTER(?peoplePerSqM > 0.431 && ?peoplePerSqM <= 0.7194) }
    OPTIONAL { ?level tsc:hasName "D" . FILTER(?peoplePerSqM > 0.7194 && ?peoplePerSqM <= 1.0753) }
    OPTIONAL { ?level tsc:hasName "E" . FILTER(?peoplePerSqM > 1.0753 && ?peoplePerSqM <= 2.1739) }
    OPTIONAL { ?level tsc:hasName "F" . FILTER(?peoplePerSqM > 2.1739) }
}