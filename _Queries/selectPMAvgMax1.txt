PREFIX tsc: <http://paper.9bon.org/ontologies/smartcity/0.2#>
PREFIX sta: <http://paper.9bon.org/ontologies/sensorthings/1.1#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?areaName ?latestResultTime ?latestPM ?avgPM
WHERE {
    {
        SELECT ?areaName (AVG(?pm) AS ?avgPM)
        WHERE {
            ?area tsc:hasName ?areaName .
            ?area tsc:hasThing/sta:hasMultiDatastream/sta:hasIndexpoint ?indexpoint .
            ?indexpoint sta:pointToMultiObservedProperty ?obsProp .
            ?obsProp sta:hasname "pm10"^^xsd:string.
            ?indexpoint sta:pointToresult ?resultResource .
            ?resultResource sta:hasvalue ?pm .
        }
        GROUP BY ?areaName
        ORDER BY DESC(?avgPM)
        LIMIT 1
    }

    ?area tsc:hasName ?areaName .
    ?area tsc:hasThing/sta:hasMultiDatastream/sta:hasIndexpoint ?indexpoint .
    ?indexpoint sta:pointToMultiObservedProperty ?latestObsProp .
    ?latestObsProp sta:hasname "pm10"^^xsd:string.
    ?indexpoint sta:pointToresult ?latestResultResource .
    ?latestResultResource sta:hasvalue ?latestPM ;
                        sta:isresultByObservation/sta:hasresultTime ?latestResultTime .
}
ORDER BY DESC(?latestResultTime)
LIMIT 1
